import groovy.json.JsonSlurper

// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply plugin: 'base'

buildscript {

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

def reactRepositoryPath = "$rootDir/repo"
def graphQlFilesPath = './xnative/src/main/res/raw'

allprojects {
    repositories {
        google()
        jcenter()

        maven {
            url "$reactRepositoryPath"
        }

    }
}

task removeGraphQlFiles(type: Delete) {
    delete graphQlFilesPath
}

task removeReactArchives(type: Delete) {
  delete reactRepositoryPath
}

clean.dependsOn(removeGraphQlFiles)
clean.dependsOn(removeReactArchives)

task generateReactArchives {
    subprojects { subproject ->
        apply plugin: 'maven'
        def reactSubprojects = [
            "xnative-rn-svg",
            "xnative-rn-webview",
            "xnative-rn-device-info",
            "xnative-rn-community-netinfo",
            "xnative-rn-community-art",
        ]
        def tcnPackageJson = new JsonSlurper().parseText(file("$rootDir/../../package.json").text)

        if (reactSubprojects.contains(subproject.name)) {
            task generateReactArchive(type: Upload) {
                configuration = subproject.configurations.archives
                repositories.mavenDeployer {
                    repository url: "file://${reactRepositoryPath}"

                    pom.project {
                        groupId 'uk.co.thetimes'
                        artifactId subproject.name
                        version tcnPackageJson.version
                    }
                }
            }

            generateReactArchives.dependsOn(generateReactArchive)
        }
    }

    task reactNativeCopy(type: Copy) {
        from('../../node_modules/react-native/android')
        into(reactRepositoryPath)
    }

    task hermesCopy {
        def hermesVersion = new JsonSlurper().parseText(file("$rootDir/../../node_modules/hermes-engine/package.json").text).version

        task hermesCopyArchive(type: Copy) {
          from('../../node_modules/hermes-engine/android/hermes-release.aar')
          into("$reactRepositoryPath/com/facebook/hermes/hermes-engine/$hermesVersion")
          rename('hermes-release.aar', "hermes-engine-${hermesVersion}.aar")
        }

        task createHermesPom {
          apply plugin: 'maven'

          description "Generates pom file for Hermes Engine"
          pom {
            project {
              groupId 'com.facebook.hermes'
              artifactId 'hermes-engine'
              version hermesVersion
              packaging 'aar'
            }
          }.writeTo("$reactRepositoryPath/com/facebook/hermes/hermes-engine/$hermesVersion/hermes-engine-${hermesVersion}.pom")
        }
        hermesCopy.dependsOn(hermesCopyArchive)
        hermesCopy.dependsOn(createHermesPom)
    }
    //pack the graphql files in the xnative.aar library so they can be reused by the android app
    task graphQlCopy(type: Copy) {
        from('../../packages/provider-queries/src/')
        include "**/*.graphql"
        into(graphQlFilesPath)
    }

    generateReactArchives.dependsOn(hermesCopy)
    generateReactArchives.dependsOn(reactNativeCopy)
    generateReactArchives.dependsOn(graphQlCopy)
}
